#!/usr/bin/perl -w
# LICENSE:
# Copyright 2012, Lucien Grondin.  All rights reserved.  
# 
# This program is free software; you can redistribute it and/or modify it under
# the same terms as Perl itself (L<perlgpl>, L<perlartistic>).
use strict;
use warnings;
use DB_File;
use CGI qw(:standard);
use Storable qw(thaw);
use IO::Uncompress::Bunzip2 qw(bunzip2 $Bunzip2Error);

use utf8;
use Encode qw(encode decode);

use constant HTML_HOME => p a { -href => $ENV{SCRIPT_NAME} }, 'back home';

sub pretreatment;
sub posttreatment;
sub get_article;

BEGIN { print header(-charset=>'utf-8'), start_html('Offline Wikipedia') }
END { print "\n" x 4, HTML_HOME, end_html }

my $project  = param('project') || 'frwiki';
if ( param 'search' ) {
    my $search  = param('search');
    my $grep = 
    -f "$project.titles" ? [ 'grep', '' ] :
    -f "$project.titles.gz" ? [ qw(zgrep .gz) ] :
    -f "$project.titles.xz" ? [ qw(xzgrep  .xz) ] :
    die p "No title file in CGI directory for project $project";
    open my $result, '-|', $grep->[0], '-i', $search, $project.".titles" . $grep->[1];
    my @result = <$result>;
    print  p [
	@result ?
	scalar(@result). " result(s) for \"$search\":" .
	ul( map li(a({ -href=>"?project=$project&title=$_" }, $_)), @result )
	: "No result for '$search' on $project."
    ];
}
elsif ( my $title = param 'title' ) {
    use DB_File;
    use Digest::MD5 qw(md5);
    eval {
	if (my ($article, $redirection) = get_article $title) {
	    my $prefix = "?project=$project&title=";
	    use Text::MediawikiFormat qw(wikiformat);
	    print h1($redirection // $title),
	    encode 'utf8', join "\n",
	    posttreatment(wikiformat pretreatment($article), {}, {prefix=>$prefix} ),
	    hr,
	    start_form(-action=>"?project=$project&title=$title"),
	    hidden(-name=>'title', -value=>$title),
	    hidden(-name=>'project', -value=>$project),
	    textarea(
		-name=> 'modif',
		-value=> $article,
		-rows=>10,
		-cols=>80,
	    ), br,
	    submit,
	    end_form
	    ;
	}
    };
    print p $@ if $@;
}
else {
    print
    start_form,
    radio_group(
	-name=>'project',
	-values=>[ map s/\.zdb$//r, glob '*.zdb' ],
	-default=>'frwiki'
    ),
    br, 'search:', textfield('search'),
    br, submit,
    end_form
    ;
}

sub pretreatment {
    shift 
    =~ s/\{\{(
    voir(?:\s(?:homonym(?:es|ie)|aussi))?               |
    (?:ébauche|détail|loupe)                                  |
    article[\s_](?:détaillé|principal|connexe)   
    )\|\s*([^{}]*)\}\}/($1: '''[[$2]]''')/gixr
    =~ s,\{\{(Japonais|Hébreu|Chinois|nihongo)\|([^|]*)\|([^}]*)\}\},$2<sup><small>([[$1|$3]])</small></sup>,gr
    =~ s/\{\{formatnum:(\d+)\}\}/$1/gir
    =~ s,\{\{e\}\},<sup>e</sup>,gir
    =~ s/\{\{infobox [^{}]*\}\}//gir
    =~ s/\{\{confusion\|texte=([^{}]*)\}\}/$1\n/gir
#    =~ s/\\left\[/〔/gr =~ s/\\right\]/〕/gr
#    =~ s/\\Rightarrow\b/ ==> /gr   
#    =~ s/\\ge\b/≥/gr =~ s/\\le\b/≤/gr
#    =~ s/\\varepsilon\b/ϵ/gr
#    =~ s/\\alpha\b/α/gr =~ s/\\beta\b/β/gr =~ s/\\delta\b/δ/gr =~ s/\\epsilon\b/ε/gr
#    =~ s/\\big([\(\)])/$1/gr
#    =~ s/\\sum_/∑_/gr
#    =~ s/\\in\b/∈/gr
#    =~ s/\\quad\b/  /gr =~ s/\\forall/∀/gr =~ s/\\exists\b/∃/gr =~ s/\\infty/∞/gr
}

sub posttreatment {
    my $_ = shift;

    my $references_mark = qr/(?:(?:&lt|<);references\s*\/(?:&gt;|>)|{{Références}})/;
    # Dealing with references
    if ( /$references_mark/s ) {
	my (@ref, %name);
	s/&lt;ref(?:\s+name=(?<name>[^\/]*?)\s*)?(?<!\/)&gt;(?<text>.*?)&lt;\/ref&gt;
	/
	push @ref, $+{text};
	$name{$+{name}} = @ref + 1 if $+{name};
	sprintf "<sup><a href=#ref%d>%d<\/a><\/sup>", (scalar(@ref),) x 2;
	/gsex;

	s/&lt;ref\s+name=([^\/]*?)\s*\/&gt;/
	sprintf "<sup><a href=#ref%d>%d<!-- DEBUG: |%s| --><\/a><\/sup>", ($name{$1},) x 2, $1
	/gsex;

	my $i;
	s/$references_mark
	/ol li [ map { "<a name=ref". ++$i . ">$_<\/a>" } @ref ]
	/sex;
    }

    return $_;

    #s,\{\{voir Wiktionnaire\|([^{}]*)\}\},Wiktionnaire: <a href="http:://localhost/cgi-bin/wiki?project=frwikt&title=$1">$1</a>,gi
    #s,&lt;ref(?: (?:group|name)=.*)?&gt;([^&]*)&lt;/ref&gt;,<!-- $1 -->,g
#   # chess diagrams
#   s/(?<=\|)[\sx]+(?=\|)/ /g
#   s/(?<=\|)pd(?=\|)/♙/gr =~ s/(?<=\|)pl(?=\|)/♟/g
#   s/(?<=\|)rd(?=\|)/♖/gr =~ s/(?<=\|)rl(?=\|)/♜/g
#   s/(?<=\|)nd(?=\|)/♘/gr =~ s/(?<=\|)nl(?=\|)/♞/g
#   s/(?<=\|)bd(?=\|)/♗/gr =~ s/(?<=\|)bl(?=\|)/♝/g
#   s/(?<=\|)qd(?=\|)/♕/gr =~ s/(?<=\|)ql(?=\|)/♛/g
#   s/(?<=\|)kd(?=\|)/♔/gr =~ s/(?<=\|)kl(?=\|)/♚/g
}
  
sub get_article {
    my $title = shift;
    $title = ucfirst $title if $project =~ /wiki$/;
    tie my %index, 'DB_File', "$project.index", O_RDONLY
	or die "could not open wiki index ($project.index)";
    my $location;
    for ( map { $_, ucfirst($_), encode 'utf8', ucfirst decode 'utf8', $_ } $title, encode 'utf8', $title ) {
	my $index = length($_) > 16 ? md5 $_ : $_;
	next unless exists $index{$index};
	($title, $location) = ($_, $index{$index});
	last;
    }
    die "could not find article '$title'" unless $location;
    my ($seek, $idx) = unpack 'LC', $location;
    open my $db, "< $project.db";
    seek $db, $seek, 0;
    read $db, my $length, 4;
    read $db, my $z, unpack 'L', $length;
    close $db;
    bunzip2 \($z, my $block);
    die $Bunzip2Error if $Bunzip2Error;
    my $article = (thaw $block)->[$idx];
    if ($article =~ /^#REDIRECT(?:ION)?\s*\[\[(?<redir>[^\]]*)\]\]$/i) {
	my $redir = $+{redir};
	return "<!-- redirected from '@{[decode 'utf8', $title]}' -->\n" .
	get_article($redir), encode 'utf8', $redir;
    } else { return $article }
}
